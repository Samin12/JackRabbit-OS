import java.util.Properties

plugins { id("com.android.application") }

val appVersion = Properties().apply {
    rootProject.file("app-version.properties").inputStream().use(::load)
}

android {
    namespace = "com.resonolabs.voice"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.resonolabs.voice"
        minSdk = 31
        targetSdk = 36
        ndk { abiFilters += listOf("arm64-v8a") }
        versionCode = appVersion.getProperty("VERSION_CODE").toInt()
        versionName = appVersion.getProperty("VERSION_NAME")
    }

    signingConfigs {
        create("sharedDebug") {
            // Explicitly consume the shared Android debug keystore restored in CI
            // (~/.android/debug.keystore) instead of relying on Gradle's implicit
            // default, which regenerates a fresh key when the file is absent.
            storeFile = file(System.getProperty("user.home") + "/.android/debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    buildTypes {
        debug {
            signingConfig = signingConfigs.getByName("sharedDebug")
            applicationIdSuffix = ".engineering"
            versionNameSuffix = "-debug"
        }
        release {
            isMinifyEnabled = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

dependencies {
    implementation(project(":core:design"))
    implementation(project(":core:input"))
    implementation(project(":core:power"))
    implementation(project(":feature:settings"))
    implementation(project(":feature:voice"))
    implementation(project(":feature:cards"))
    implementation(project(":feature:camera"))
    implementation(project(":feature:creation-import"))
    implementation(project(":feature:background-run"))
    implementation(project(":feature:genui"))
    implementation(project(":feature:t3"))
    implementation(project(":feature:compose"))
    implementation(project(":core:motor"))
    implementation(project(":runtime-host"))
    testImplementation("junit:junit:4.13.2")
}
